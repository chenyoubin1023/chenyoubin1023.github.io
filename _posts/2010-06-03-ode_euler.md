---
title: Méthode d'Euler explicite
categories:
  - 数学
---

本文使用 Scilab 运用 Méthode d'Euler explicite 计算微分方程的数值解，如果我有时间我会在这里发一个 Scilab 使用的教程系列博文。相比体积庞大理论上价格不菲的 Matlab，Scilab 不仅开源免费，并且具备 Matlab 拥有的大多数功能（Matlab 优势仅仅在于具有很多的工具箱，不过 Scilab 也在逐渐拥有人气，工具箱也在不断增加），作为学生而言是足够了。

``` matlab
function [t,y]=euler(y0,T,N,f)
  // ***** 程序说明 *****
  // 数值计算微分方程y'=f(t,y(t)), 初始条件t0=0, y(t0)=y0
  // 计算区间[t0,T], 即[0,T]
  // 将区间[0,T]分为N个子区间, 有t0=0, tN=T
  // 输出时间序列t, 计算得微分方程解y
  // ***** 程序开始 *****
  h=T/N; // 步长
  t=0:h:T; // 提供计算点
  y(1)=y0; // 定义初始值
  for i=1:N // 开始计算
    y(i+1)=y(i)+h*f(t(i),y(i)); // 显性欧拉法关键的一步
  end
endfunction
    
// 试验, 设y'(t)=-λt
y0=1; // 提供初始值
T=5; // 提供计算区间
N=20; // 提供区间划分数
lambda=1; // λ=1
function ydot=f(t,y) // 提供函数f
  ydot=-lambda*y;
endfunction
// 调用 euler 函数进行计算
[t,y]=euler(y0,T,N,f);
// 与精确值进行比较
plot(t,y,'--',t,y0*exp(-lambda*t));
legend(['Euler explicite';'Exact']);
```

Méthode d'Euler explicite 是一个收敛（Convergente）的计算方法，即步长 $$h$$ 逼近0之后，那么我们得到的近似解将与精确解完全一致，大家可以在代码中改变 $$N$$ 的大小试试看。